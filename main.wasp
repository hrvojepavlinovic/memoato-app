app memoato {
  wasp: {
    version: "^0.20.1"
  },
  title: "memoato",
  head: [
    "<link rel='icon' href='/favicon.ico' />",
    "<link rel='icon' type='image/png' sizes='32x32' href='/favicon-32x32.png' />",
    "<link rel='icon' type='image/png' sizes='16x16' href='/favicon-16x16.png' />",
    "<link rel='apple-touch-icon' sizes='180x180' href='/apple-touch-icon.png?v=7' />",
    "<link rel='manifest' href='/manifest.json?v=7' />",
    "<meta name='theme-color' content='#ffffff' />",
    "<meta name='description' content='A fast, minimal habit tracker for ADHD brains: quick input, clear progress, fewer decisions.' />",
    "<meta property='og:site_name' content='memoato' />",
    "<meta property='og:title' content='memoato' />",
    "<meta property='og:description' content='A fast, minimal habit tracker for ADHD brains: quick input, clear progress, fewer decisions.' />",
    "<meta property='og:url' content='https://app.memoato.com' />",
    "<meta name='mobile-web-app-capable' content='yes' />",
    "<meta name='apple-mobile-web-app-capable' content='yes' />",
    "<meta name='apple-mobile-web-app-status-bar-style' content='default' />",
    "<meta name='apple-mobile-web-app-title' content='memoato' />",
    "<script>(function(){try{var ls='';try{ls=(localStorage.getItem('memoato:theme')||'').trim();}catch(e){}var m=/(?:^|; )memoato_theme=([^;]*)/.exec(document.cookie||'');var c=m?decodeURIComponent(m[1]||''):'';var t=(ls==='dark'||ls==='light')?ls:((c==='dark'||c==='light')?c:'light');var dark=t==='dark';var el=document.documentElement;el.classList.toggle('dark',dark);el.style.colorScheme=dark?'dark':'light';var meta=document.querySelector(\"meta[name='theme-color']\");if(meta)meta.setAttribute('content',dark?'#0A0A0A':'#ffffff');}catch(e){}})();</script>",
  ],
  auth: {
    userEntity: User,
    methods: {
      email: {
        fromField: {
          name: "Memoato",
          email: "login@memoato.com"
        },
        userSignupFields: import { userSignupFields } from "@src/auth/email/userSignupFields",
        emailVerification: {
          clientRoute: EmailVerificationRoute,
        },
        passwordReset: {
          clientRoute: PasswordResetRoute,
        }
      },
      google: {
        configFn: import { getConfig } from "@src/auth/google",
        userSignupFields: import { userSignupFields } from "@src/auth/google",
      },
    },
    onAuthSucceededRedirectTo: "/",
    onAuthFailedRedirectTo: "/login",
  },
  emailSender: {
    provider: SMTP,
    defaultFrom: {
      name: "Memoato",
      email: "login@memoato.com",
    }
  },
  client: {
    rootComponent: import { App } from "@src/App",
  },
  server: {
    setupFn: import { setupPublicStatsRoutes } from "@src/public/serverSetup"
  }
}

// #region Auth
route LoginRoute { path: "/login", to: LoginPage }
page LoginPage {
  component: import { LoginPage } from "@src/auth/email/LoginPage"
}

route SignupRoute { path: "/signup", to: SignupPage }
page SignupPage {
  component: import { SignupPage } from "@src/auth/email/SignupPage"
}

route RequestPasswordResetRoute { path: "/request-password-reset", to: RequestPasswordResetPage }
page RequestPasswordResetPage {
  component: import { RequestPasswordResetPage } from "@src/auth/email/RequestPasswordResetPage",
}

route PasswordResetRoute { path: "/password-reset", to: PasswordResetPage }
page PasswordResetPage {
  component: import { PasswordResetPage } from "@src/auth/email/PasswordResetPage",
}

route EmailVerificationRoute { path: "/email-verification", to: EmailVerificationPage }
page EmailVerificationPage {
  component: import { EmailVerificationPage } from "@src/auth/email/EmailVerificationPage",
}
// #endregion Auth

// #region Focus
route HomeRoute { path: "/", to: HomePage }
page HomePage {
  authRequired: true,
  component: import { HomePage } from "@src/focus/HomePage"
}

route OnboardingRoute { path: "/onboarding", to: OnboardingPage }
page OnboardingPage {
  authRequired: true,
  component: import { OnboardingPage } from "@src/onboarding/OnboardingPage"
}

route TimelineRoute { path: "/timeline", to: TimelinePage }
route TimelineDayRoute { path: "/timeline/:occurredOn", to: TimelinePage }
page TimelinePage {
  authRequired: true,
  component: import { TimelinePage } from "@src/timeline/TimelinePage"
}

route SudoRoute { path: "/sudo", to: SudoPage }
page SudoPage {
  component: import { SudoPage } from "@src/admin/SudoPage"
}

route ProfileRoute { path: "/profile", to: ProfilePage }
page ProfilePage {
  authRequired: true,
  component: import { ProfilePage } from "@src/profile/ProfilePage"
}

route RemindersRoute { path: "/reminders", to: RemindersPage }
page RemindersPage {
  authRequired: true,
  component: import { RemindersPage } from "@src/pwa/RemindersPage"
}

route ConfirmEmailChangeRoute { path: "/profile/email-change", to: ConfirmEmailChangePage }
page ConfirmEmailChangePage {
  component: import { ConfirmEmailChangePage } from "@src/profile/ConfirmEmailChangePage"
}

route ConfirmDeleteAccountRoute { path: "/profile/delete-account", to: ConfirmDeleteAccountPage }
page ConfirmDeleteAccountPage {
  component: import { ConfirmDeleteAccountPage } from "@src/profile/ConfirmDeleteAccountPage"
}

route NewCategoryRoute { path: "/categories/new", to: NewCategoryPage }
page NewCategoryPage {
  authRequired: true,
  component: import { NewCategoryPage } from "@src/focus/NewCategoryPage"
}

route EditCategoryRoute { path: "/categories/:categorySlug/edit", to: EditCategoryPage }
page EditCategoryPage {
  authRequired: true,
  component: import { EditCategoryPage } from "@src/focus/EditCategoryPage"
}

route CategoryRoute { path: "/c/:categorySlug", to: CategoryPage }
page CategoryPage {
  authRequired: true,
  component: import { CategoryPage } from "@src/focus/CategoryPage"
}

route PublicUserRoute { path: "/u/:username", to: PublicUserPage }
page PublicUserPage {
  component: import { PublicUserPage } from "@src/public/PublicUserPage"
}

route PublicUserCategoryRoute { path: "/u/:username/c/:categorySlug", to: PublicUserCategoryPage }
page PublicUserCategoryPage {
  component: import { PublicUserCategoryPage } from "@src/public/PublicUserCategoryPage"
}

query getCategories {
  fn: import { getCategories } from "@src/focus/queries",
  entities: [User, Category, Event]
}

query getPublicUserDashboard {
  fn: import { getPublicUserDashboard } from "@src/public/dashboardQueries",
  entities: [User, Category, Event]
}

query getPublicUserCategorySeries {
  fn: import { getPublicUserCategorySeries } from "@src/public/dashboardQueries",
  entities: [User, Category, Event]
}

query getPublicUserCategoryLineSeries {
  fn: import { getPublicUserCategoryLineSeries } from "@src/public/dashboardQueries",
  entities: [User, Category, Event]
}

query getCategorySeries {
  fn: import { getCategorySeries } from "@src/focus/queries",
  entities: [User, Event, Category]
}

query getCategoryLineSeries {
  fn: import { getCategoryLineSeries } from "@src/focus/queries",
  entities: [Event, Category]
}

query getCategoryEvents {
  fn: import { getCategoryEvents } from "@src/focus/queries",
  entities: [User, Event, Category]
}

query getCategoryTemplates {
  fn: import { getCategoryTemplates } from "@src/focus/templateQueries",
  entities: [CategoryTemplate]
}

query getSudoOverview {
  fn: import { getSudoOverview } from "@src/admin/queries",
  entities: [User, Category, Event]
}

query getProfile {
  fn: import { getProfile } from "@src/profile/queries",
  entities: [User]
}

query getDayEvents {
  fn: import { getDayEvents } from "@src/timeline/queries",
  entities: [Event, Category]
}

query getPublicTotals {
  fn: import { getPublicTotals } from "@src/public/queries",
  entities: [User, Category, Event]
}

action ensureDefaultCategories {
  fn: import { ensureDefaultCategories } from "@src/focus/actions",
  entities: [Category, Event]
}

action createEvent {
  fn: import { createEvent } from "@src/focus/actions",
  entities: [Event, Category]
}

action updateEvent {
  fn: import { updateEvent } from "@src/focus/actions",
  entities: [Event, Category]
}

action deleteEvent {
  fn: import { deleteEvent } from "@src/focus/actions",
  entities: [Event]
}

action createCategory {
  fn: import { createCategory } from "@src/focus/actions",
  entities: [Category]
}

action updateCategory {
  fn: import { updateCategory } from "@src/focus/actions",
  entities: [Category]
}

action deleteCategory {
  fn: import { deleteCategory } from "@src/focus/actions",
  entities: [Category, Event]
}

action setCategoryOrder {
  fn: import { setCategoryOrder } from "@src/focus/actions",
  entities: [Category]
}

action resetCategoryOrder {
  fn: import { resetCategoryOrder } from "@src/focus/actions",
  entities: [Category]
}

action updateProfile {
  fn: import { updateProfile } from "@src/profile/actions",
  entities: [User]
}

action setNextUpEnabled {
  fn: import { setNextUpEnabled } from "@src/profile/actions",
  entities: [User]
}

action setThemePreference {
  fn: import { setThemePreference } from "@src/profile/actions",
  entities: [User]
}

action setQuickLogFabSide {
  fn: import { setQuickLogFabSide } from "@src/profile/actions",
  entities: [User]
}

action setHomeCategoryLayout {
  fn: import { setHomeCategoryLayout } from "@src/profile/actions",
  entities: [User]
}

action setActiveKcalRollupMode {
  fn: import { setActiveKcalRollupMode } from "@src/profile/actions",
  entities: [User]
}

action setPublicStatsEnabled {
  fn: import { setPublicStatsEnabled } from "@src/profile/actions",
  entities: [User, Category]
}

action rotatePublicStatsToken {
  fn: import { rotatePublicStatsToken } from "@src/profile/actions",
  entities: [User]
}

action setPublicStatsCategories {
  fn: import { setPublicStatsCategories } from "@src/profile/actions",
  entities: [User, Category]
}

action requestEmailChange {
  fn: import { requestEmailChange } from "@src/profile/actions",
  entities: [User]
}

action confirmEmailChange {
  fn: import { confirmEmailChange } from "@src/profile/actions"
}

action sendPasswordResetForCurrentUser {
  fn: import { sendPasswordResetForCurrentUser } from "@src/profile/actions"
}

action requestAccountDeletion {
  fn: import { requestAccountDeletion } from "@src/profile/actions"
}

action confirmAccountDeletion {
  fn: import { confirmAccountDeletion } from "@src/profile/actions"
}

action exportMyData {
  fn: import { exportMyData } from "@src/profile/export",
  entities: [User, Category, Event]
}

// analytics is handled client-side via Databuddy script in <head>
// #endregion Focus

// Catch-all 404
route NotFoundRoute { path: "/*", to: NotFoundPage }
page NotFoundPage {
  component: import { NotFoundPage } from "@src/NotFoundPage"
}
